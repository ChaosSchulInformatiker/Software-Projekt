plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.10'

    id 'com.xcporter.metaview' version '0.0.4'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'tk.q11mk'
version '0.2'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext {
    ktorVersion = '1.5.4'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-cio:$ktorVersion"
    implementation "io.ktor:ktor-serialization:$ktorVersion"

    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'

    implementation "com.googlecode.json-simple:json-simple:1.1.1"

    implementation "mysql:mysql-connector-java:8.0.25"
}

compileJava {
    options.encoding = 'UTF-8'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '16'
        freeCompilerArgs = freeCompilerArgs + "-Xallow-result-return-type"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'tk.q11mk.Server'
    }
}

shadowJar {
    def sout = new ByteArrayOutputStream()
    exec {
        executable 'git'
        args('rev-parse', '--short', 'HEAD')
        standardOutput(sout)
    }
    def ba = sout.toByteArray()
    def hba = new byte[ba.length - 1]
    System.arraycopy(ba, 0, hba, 0, hba.length)
    def hash = new String(hba)
    archiveName "MaristenPlaner-Server-v$project.version-${hash}.jar"
    //archiveName jarName
}

generateUml {
    classTree {}
    functionTree {}
}